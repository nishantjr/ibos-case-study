load ../systems/ibos-preds.maude
load ../../rltool-lib.maude

mod IMPORTS is
  pr CONFLUENCE-TOOLS .
  pr SIMPLE-CRITPAIR .
endm

mod TEST is
  pr IMPORTS *
    (op __ : EquationSet EquationSet -> EquationSet to _eq_,
     op __ : SubsortDeclSet SubsortDeclSet -> SubsortDeclSet to _sbs_
    ) .

  op pid : -> Module .
  eq pid = upModule('PROC-ID,true) .

  op mfe : -> Module .
  eq mfe = upModule('MSG-FVP-EXT,true) .

  op safe-mfe : -> Module [memo] .
  eq safe-mfe = deleteIdEqs(removeIds(mfe,non-handled)) .

  op afe : -> Module .
  eq afe = upModule('ATTRIBUTE-FVP-EXT,true) .

  op ibos : -> Module .
  eq ibos = upModule('IBOS-PREDS,true) .

  op noid-ibos : -> Module .
  eq noid-ibos = removeIds(ibos,non-handled) .

  op safe-ibos : -> Module [memo] .
  eq safe-ibos = deleteIdEqs(removeIds(ibos,non-handled)) .

  op round1 : -> CritPairSet .
  eq round1 =
    pendingCPs(
      crcContextJoinableAndUnfeasibleCPs(noid-ibos,
        toStdCritPairs(
          renameCPs(
            subsume(ibos,
              simplify(ibos,
                getSimpleCritPairs(safe-ibos,
                  ('p['NWC:NetWebProcConfig,'WIS:WebProcInfoSet,'NIS:NetProcInfoSet,'M:Message?] |->
                     ('nwp-reqattrs?  ['NWC:NetWebProcConfig] = 'true.Bool  /\
                      'conf-attr-dupl?['NWC:NetWebProcConfig] = 'false.Bool /\
                      'weblabels-dupl?['WIS:WebProcInfoSet]   = 'false.Bool /\
                      'netlabels-dupl?['NIS:NetProcInfoSet]   = 'false.Bool /\
                      'conf-dupl?     ['NWC:NetWebProcConfig] = 'false.Bool),
                   'render-consistent?['NWC:NetWebProcConfig] |->
                     ('conf-attr-dupl?['NWC:NetWebProcConfig] = 'false.Bool))))))))) .

  op round2 : -> CritPairSet .
  eq round2 =
    pendingCPs(
      crcContextJoinableAndUnfeasibleCPs(noid-ibos,
        simpCP(ibos,
          partialVarUnif(noid-ibos,
            simpCP(ibos,round1))))) .

  op round3 : -> CritPairSet .
  eq round3 =
    pendingCPs(
      crcContextJoinableAndUnfeasibleCPs(removeIds(ibos,non-handled),
        simpCP(ibos,
          transform(round2,
            { split-cover('fresh-netlabel?['network['_+_['&0:Nat,'&1:Nat]],'&4:NetProcInfoSet],
              ('true.Bool  : skip )
              ('false.Bool : skip )) }
            { split-cover('fresh-netlabel?['network['_+_['&0:Nat,'&2:NzNat]],'&4:NetProcInfoSet],
              ('true.Bool  : skip )
              ('false.Bool : skip )) }
            { split-cover('&0:Nat,
              ('&0:NzNat : skip)
              ('0.Nat    : skip)) }
            { split-cover('&1:NzNat,
              ('1.NzNat : skip)
              ('_+_['1.NzNat,'&1:NzNat] : skip)) }
            { split-cover('fresh-weblabel?['webapp['_+_['&1:Nat,'&2:Nat]],'&4:WebProcInfoSet],
              ('true.Bool  : skip )
              ('false.Bool : skip )) }
            { split-cover('fresh-weblabel?['webapp['_+_['&0:Nat,'&3:NzNat]],'&4:WebProcInfoSet],
              ('true.Bool  : skip )
              ('false.Bool : skip )) }
            { split-cover('&0:Nat,
              ('&0:NzNat : skip)
              ('0.Nat    : skip)) }
            { split-cover('&1:NzNat,
              ('1.NzNat : skip)
              ('_+_['1.NzNat,'&1:NzNat] : skip)) }
            { split-cover('&2:ConcNetProcId,
              ('network['&99:Nat] : skip)) }
            { split-cover('&1:ConcWebProcId,
              ('webapp['&99:Nat] : skip)) }
            nil)))) .
endm

--- set print conceal on .
--- print conceal _eq_ _sbs_ .
--- NOTE: based on source code check and reading CRC paper,
---       all context-joinability/unfeasibility checks only use search
---       so they can occur in the original module in this case
---
--- SIMPLE EXAMPLE
---
--- red pendingCPs(crcContextJoinableAndUnfeasibleCPs(safe-mfe,toStdCritPairs(renameCPs(subsume(mfe,simplify(mfe,getSimpleCritPairs(safe-mfe))))))) .
---
--- FULL TEST (optionally restricted to opdecls listed)
--- generating CPs for SOP predicate with filter: only 15 minutes and 51 seconds
red round3 .

quit
