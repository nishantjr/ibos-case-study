load ../../systems/ibos.maude

--- NOTES:
--- [1] local simplifications in formula structure are unsound when shared variables
---     are present which exist outside of the fragment that are lost --- this is
---     because the information regarding the variables is lost when the constraint
---     is solved --- some possibilities:
---     a. make variables into constants, do reductions, and then lift reduced items
---        into my theory so they can be used in later reduction steps
---     b. do reductions over formula-functor lifted algebras --- here we can express
---        express constraints about variables directly in our rules
---     c. make two copies of the formula --- one non-reduced version and one reduced
---        version --- then be careful to always allow reductions for each goal locally
---        have access to the unreduced parts that were assumed (or something like that)
---     d. record each reduction step so we keep around the set of all true facts that
---        we can assume --- then give our other reductions access to that set
--- [2] because messages may be dynamically added to the list, we need to ensure
---     that the metadata in the kernel WIS/NIS has the correct shape so that when
---     this webapp receives a message of the form that we care about, we know the
---     metadata will look like what we need to complete the proof
--- [3] since the only way the message list can increase is by the rule
---     kernelForwardsOPMessage, it SHOULD be enough to require:
---     a. kernel's currentlyHandled() attribute to be correct
---     b. kernel's message policy is locked down to the minimum necessary
---        (this way we can't instantiate the message policy with weird connections
---         and send MSG-FETCH/RETURN-URLs down those connections) ---
---        thus, we choose the default initial policy:
---        (webapp,network,MSG-FETCH-URL)
---        (network,webapp,MSG-RETURN-URL)
---        (ui,webapp,MSG-SWITCH-TAB)
---        (ui,webapp,MSG-NEW-URL)
---     However, requiring the webapps to always be reflected properly in the
---     kernel metadata just means that we will never have a terminating state
---     where the kernel tried to send a message and could not because no webapp
---     was there to receive it.
---     Also, probably the narrowing time is similar... maybe.
--- [4] if handledCurrently contains some message addressed to non-existent processes,
---     we don't care---that is a weird state, but it doesn't concern us---what concerns
---     us is that if the message is received by a real webapp, then it will satisfy the
---     conditions above

mod IBOS-EXT is
  pr IBOS-STOP .

  var C : Configuration .
  var W N M : Nat .
  var P : NzNat .
  var A A' : AttributeSet .
  var NA : NeAttributeSet .
  var NNPI : NonNetProcId .
  var NWAI : NonWebAppId .
  var PI PI' : ProcessId .
  var WAI : WebAppId .
  var MSGL : MessageList .
  var L : Label .

  --- equality predicate for message types
  op _~m_ : MsgType MsgType -> Bool [comm metadata "86"] .
  eq X:MsgType      ~m X:MsgType      = true .
  eq MSG-NEW-URL    ~m MSG-FETCH-URL  = false .
  eq MSG-NEW-URL    ~m MSG-RETURN-URL = false .
  eq MSG-NEW-URL    ~m MSG-SWITCH-TAB = false .
  eq MSG-FETCH-URL  ~m MSG-RETURN-URL = false .
  eq MSG-FETCH-URL  ~m MSG-SWITCH-TAB = false .
  eq MSG-RETURN-URL ~m MSG-SWITCH-TAB = false .

  --- for simplified case analysis
  sort NonRetFetchMsg NonRetMsg NonFetchMsg .
  subsort NonRetFetchMsg < NonRetMsg NonFetchMsg < MsgType .
  op MSG-NEW-URL    : -> NonRetFetchMsg [ctor metadata "44"] .
  op MSG-FETCH-URL  : -> NonRetMsg      [ctor metadata "45"] .
  op MSG-RETURN-URL : -> NonFetchMsg    [ctor metadata "46"] .
  op MSG-SWITCH-TAB : -> NonRetFetchMsg [ctor metadata "47"] .
  ----------------------------------------------------
  eq MSG-RETURN-URL ~m M:NonRetMsg   = false .
  eq MSG-FETCH-URL  ~m M:NonFetchMsg = false .

  --- duplicates can ignore attributes
  op dupl : Configuration -> Bool [metadata "87"] .
  eq dupl(< PI | A > < PI | A' > C) = true .
  eq dupl(< PI | toKernel(MSGL),      A > C) = dupl(< PI | A > C) .
  eq dupl(< PI | fromKernel(MSGL),    A > C) = dupl(< PI | A > C) .
  eq dupl(< PI | activeWebapp(WAI),   A > C) = dupl(< PI | A > C) .
  eq dupl(< PI | displayedContent(L), A > C) = dupl(< PI | A > C) .
  eq dupl(< network(N) | A > < kernel    | nextNetworkProc(N + 1), A' > C) = dupl(< kernel    | nextNetworkProc(N), A' > C) .
  eq dupl(< webapp(W)  | A > < webappmgr | nextWAN(W + 1),         A' > C) = dupl(< webappmgr | nextWAN(W),         A' > C) .
  eq dupl(< PI | A >) = false .
  eq dupl(none) = false .
 ceq dupl(< PI | none > < PI' | none > C) = dupl(< PI | none > C) and dupl(< PI' | none > C) if PI ~p PI' = false .

  op fresh-np-id : Nat Configuration -> Bool [metadata "88"] .
  eq fresh-np-id(N,     < network(N + M) | A > C) = false .
  eq fresh-np-id(N + P, < network(N)     | A > C) = fresh-np-id(N + P,C) .
  eq fresh-np-id(N,     < network        | A > C) = fresh-np-id(N,C) .
  eq fresh-np-id(N,     < NNPI           | A > C) = fresh-np-id(N,C) .
  eq fresh-np-id(N,     none                    ) = true .

  op fresh-wa-id : Nat Configuration -> Bool [metadata "89"] .
  eq fresh-wa-id(N,     < webapp(N + M) | A > C) = false .
  eq fresh-wa-id(N + P, < webapp(N)     | A > C) = fresh-wa-id(N + P,C) .
  eq fresh-wa-id(N,     < webapp        | A > C) = fresh-wa-id(N,C) .
  eq fresh-wa-id(N,     < NWAI          | A > C) = fresh-wa-id(N,C) .
  eq fresh-wa-id(N,     none                   ) = true .
endm

mod IBOS-SOP is
  pr IBOS-EXT .

  var C : Configuration .
  var W N : Nat .
  var A : AttributeSet .
  var WIS : WebappProcInfoSet .
  var NIS : NetworkProcInfoSet .
  var ML : MessageList .
  var NNPI : NonNetProcId .
  var NWAI : NonWebAppId .
  var NI : NetProcId .
  var WI : WebAppId .
  var PI PI' : PipeId .
  var NPI : NonPipeId .
  var IN OUT OUT2 : Label .
  var MT : MsgType .
  var NonFetchMT : NonFetchMsg .
  var NonRetMT : NonRetMsg .

  ---
  --- Here, we prove two kinds of invariants:
  --- 1. check that the messages in an object's message queue are
  ---    consistent with our security policy
  --- 2. check that any message in the kernel's currentlyHandled()
  ---    attribute is consistent with our security policy
  ---
  --- Where our security policy is specified by the kernel's
  --- hardwired message policy object as well as:
  --- a. network process metadata
  --- b. webapp process metadata
  ---
  --- Note to prove properties (1)-(2) actually requires a quadruple
  --- induction, i.e. we induct over:
  --- i. every network/webapp process in the soup
  --- ii. every message in the message queue of such a process
  --- iii. every network process metadatum in the kernel info set
  --- iv. every webapp process metadatum in the kernel info set
  ---
  --- One feature of this specification is that processes are
  --- never deleted --- so that the process set/process info sets
  --- always grow monotonically. This simplifies the proof procedure.
  ---

  -----------------------------------------------------------------------
  --- True if every FETCH-URL sent to every network process is consistent
  op p1 : Configuration WebappProcInfoSet NetworkProcInfoSet -> Bool [metadata "90"] .
  ---------------------------------------------------------------------------

  eq [p1-1] : p1(C < NI | fromKernel(msg(WI, NI, MSG-FETCH-URL, OUT) @ ML), A >, (pi(WI,IN),wp WIS), (pi(NI,IN,OUT),np NIS))
            = p1(C < NI | fromKernel(ML),                                   A >, (pi(WI,IN),wp WIS), (pi(NI,IN,OUT),np NIS)) .

  eq [p1-2] : p1(C < NI | fromKernel(msg(WI, NI, MSG-FETCH-URL, OUT)),      A >, (pi(WI,IN),wp WIS), (pi(NI,IN,OUT),np NIS))
            = p1(C,                                                              (pi(WI,IN),wp WIS), (pi(NI,IN,OUT),np NIS)) .

  eq [p1-3] : p1(C < NI | fromKernel(msg(PI,PI',NonFetchMT,OUT) @ ML), A >, WIS, NIS)
            = p1(C < NI | fromKernel(ML),                              A >, WIS, NIS) .
  eq [p1-4] : p1(C < NI | fromKernel(msg(PI,PI',NonFetchMT,OUT)),      A >, WIS, NIS)
            = p1(C,                                                         WIS, NIS) .

  eq [p1-5] : p1(C < NI      | fromKernel(mt), A >, WIS, NIS) = p1(C, WIS, NIS) .
  eq [p1-6] : p1(C < network |                 A >, WIS, NIS) = p1(C, WIS, NIS) .
  eq [p1-7] : p1(C < NNPI    |                 A >, WIS, NIS) = p1(C, WIS, NIS) .

  eq [p1-8] : p1(none, WIS, NIS) = true .

 ceq [p1-9] : p1(C,(pi(webapp(W),IN),wp WIS),                          NIS ) = p1(C,WIS,NIS) if fresh-wa-id(W,C) .
 ceq [p1-A] : p1(C,                     WIS ,(pi(network(N),IN,OUT),np NIS)) = p1(C,WIS,NIS) if fresh-np-id(N,C) .

  -----------------------------------------------------------------------
  --- True if every RETURN-URL message sent to every webapp is consistent
  op p4 : Configuration WebappProcInfoSet NetworkProcInfoSet -> Bool [metadata "91"] .
  ---------------------------------------------------------------------------

  eq [p4-1] : p4(C < WI | fromKernel(msg(NI, WI, MSG-RETURN-URL, OUT) @ ML), A >, (pi(WI,IN),wp WIS), (pi(NI,IN,OUT),np NIS))
            = p4(C < WI | fromKernel(ML),                                    A >, (pi(WI,IN),wp WIS), (pi(NI,IN,OUT),np NIS)) .
  eq [p4-2] : p4(C < WI | fromKernel(msg(NI, WI, MSG-RETURN-URL, OUT)),      A >, (pi(WI,IN),wp WIS), (pi(NI,IN,OUT),np NIS))
            = p4(C,                                                               (pi(WI,IN),wp WIS), (pi(NI,IN,OUT),np NIS)) .

  eq [p4-3] : p4(C < WI | fromKernel(msg(PI,PI',NonRetMT,OUT) @ ML), A >, WIS, NIS) = p4(C < WI | fromKernel(ML), A >, WIS, NIS) .
  eq [p4-4] : p4(C < WI | fromKernel(msg(PI,PI',NonRetMT,OUT)),      A >, WIS, NIS) = p4(C,                            WIS, NIS) .

  eq [p4-5] : p4(C < WI     | fromKernel(mt), A >, WIS, NIS) = p4(C, WIS, NIS) .
  eq [p4-6] : p4(C < webapp |                 A >, WIS, NIS) = p4(C, WIS, NIS) .
  eq [p4-7] : p4(C < NWAI   |                 A >, WIS, NIS) = p4(C, WIS, NIS) .

  eq [p4-8] : p4(none, WIS, NIS) = true .

 ceq [p4-9] : p4(C,(pi(webapp(W),IN),wp WIS),                          NIS ) = p4(C,WIS,NIS) if fresh-wa-id(W,C) .
 ceq [p4-A] : p4(C,                     WIS ,(pi(network(N),IN,OUT),np NIS)) = p4(C,WIS,NIS) if fresh-np-id(N,C) .

  eq [p4-I1] : p4(C < WI | toKernel(ML), A >,WIS,NIS) = p4(C < WI | A >,WIS,NIS) .

  -------------------------------------------------------------------------
  --- True if every RETURN-URL sent from every network object is consistent
  --- NB: toKernel doesn't require sender match, since kernel will rewrite
  --- NB: cc1a/2a rule doesn't require the OUT labels match!!!!
  op p5 : Configuration WebappProcInfoSet NetworkProcInfoSet -> Bool [metadata "92"] .
  --------------------------------------------------------------------

  eq [p5-1] : p5(C < NI | toKernel(msg(PI, WI, MSG-RETURN-URL, OUT2) @ ML), A >, (pi(WI,IN),wp WIS), (pi(NI,IN,OUT),np NIS))
            = p5(C < NI | toKernel(ML),                                     A >, (pi(WI,IN),wp WIS), (pi(NI,IN,OUT),np NIS)) .
  eq [p5-2] : p5(C < NI | toKernel(msg(PI, WI, MSG-RETURN-URL, OUT2)),      A >, (pi(WI,IN),wp WIS), (pi(NI,IN,OUT),np NIS))
            = p5(C,                                                              (pi(WI,IN),wp WIS), (pi(NI,IN,OUT),np NIS)) .

  eq [p5-3] : p5(C < NI | toKernel(msg(PI,PI',NonRetMT,OUT) @ ML), A >, WIS, NIS) = p5(C < NI | toKernel(ML), A >, WIS, NIS) .
  eq [p5-4] : p5(C < NI | toKernel(msg(PI,PI',NonRetMT,OUT)),      A >, WIS, NIS) = p5(C,                          WIS, NIS) .

  eq [p5-5] : p5(C < NI      | toKernel(mt), A >, WIS, NIS) = p5(C, WIS, NIS) .
  eq [p5-6] : p5(C < network |               A >, WIS, NIS) = p5(C, WIS, NIS) .
  eq [p5-7] : p5(C < NNPI    |               A >, WIS, NIS) = p5(C, WIS, NIS) .

  eq [p5-8] : p5(none, WIS, NIS) = true .

 ceq [p5-9] : p5(C,(pi(webapp(W),IN),wp WIS),                          NIS ) = p5(C,WIS,NIS) if fresh-wa-id(W,C) .
 ceq [p5-A] : p5(C,                     WIS ,(pi(network(N),IN,OUT),np NIS)) = p5(C,WIS,NIS) if fresh-np-id(N,C) .

  ------------------------------------------------------------------------------
  --- True if every RETURN-URL message currently handled by kernel is consistent
  --- NB: since message is headed towards a webapp, we don't need the OUT label to be consistent
  op ch-p14 : Message WebappProcInfoSet NetworkProcInfoSet Nat -> Bool [metadata "93"] .
  -----------------------------------------------------------------

  eq ch-p14(msg(NI,WI,MSG-RETURN-URL,OUT2), (pi(WI,IN),wp WIS), (pi(NI,IN,OUT),np NIS),N) = true .
  eq ch-p14(msg(NI,WI,NonRetMT,OUT), WIS, NIS, N) = true .
 ceq ch-p14(msg(WI,NI,MT,OUT), (pi(WI,IN),wp WIS), NIS, N) = true
  if (not networkproc-for-label(IN,NIS)) or label-for-netproc(NI,NIS) ~l IN .
  eq ch-p14(none,WIS,NIS,N) = true .

  op label-for-netproc : NetProcId NetworkProcInfoSet -> Label [metadata "94"] .
  eq label-for-netproc(NI,(pi(NI,IN,OUT),np NIS)) = IN .
endm
